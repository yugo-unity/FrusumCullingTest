#pragma kernel InstancingCullLOD

#include "./InstanceData.hlsl"

uniform float3 _CameraPos;
uniform float _LODThreshold;
uniform float _TanFOV;
uniform int _InstanceCount;
uniform StructuredBuffer<InstanceData> _InstanceData;
uniform StructuredBuffer<float4> _FrustumPlanes;
uniform RWStructuredBuffer<uint> _Lod0Indexes;
uniform RWStructuredBuffer<uint> _Lod1Indexes;  

// skip for loop by considering Structured-Of-Array
// also skip near and far clips
bool TestAABBFrustum(float3 bounds, float3 boundExtents)
{
    float4 cx = bounds.xxxx;
    float4 cy = bounds.yyyy;
    float4 cz = bounds.zzzz;
    float4 distances = _FrustumPlanes[0] * cx + _FrustumPlanes[1] * cy + _FrustumPlanes[2] * cz + _FrustumPlanes[3];
    float4 ex = boundExtents.xxxx;
    float4 ey = boundExtents.yyyy;
    float4 ez = boundExtents.zzzz;
    float4 radii = abs(_FrustumPlanes[0]) * ex + abs(_FrustumPlanes[1]) * ey + abs(_FrustumPlanes[2]) * ez;

    //bool4 isCulled = (distances + radii) < float4(0, 0, 0, 0);
    bool4 isCulled = step(distances + radii, float4(0, 0, 0, 0));
    
    return any(isCulled);
}

[numthreads(64, 1, 1)]
void InstancingCullLOD(uint id : SV_DispatchThreadID)
{
    if (id > _InstanceCount)
        return;
    InstanceData data = _InstanceData[id];

    if (TestAABBFrustum(data.boundPoint, data.boundSize))
        return;
    
    float3 position = float3(data.worldMatrix[0].w, data.worldMatrix[1].w, data.worldMatrix[2].w);
    float dist = length(position - _CameraPos);
    float frustumHeight = dist * _TanFOV; // boundSize means extents here, so no need x2
    if (data.boundSize.y > (_LODThreshold * frustumHeight))
    {
        int index = _Lod0Indexes.IncrementCounter();
        _Lod0Indexes[index] = id;
    }
    else
    {
        int index = _Lod1Indexes.IncrementCounter();
        _Lod1Indexes[index] = id;
    }
}
